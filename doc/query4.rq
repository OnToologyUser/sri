PREFIX sri: <https://w3id.org/sri#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX prov: <http://www.w3.org/ns/prov#>
# Step 4 in the SRI methodology
# documented in sections 1.3.6.4 on page 130 in the final report
# available from https://data.europa.eu/doi/10.2833/41100
# "Calculate maximum impact scores"
INSERT {
    GRAPH $scoresds {
        ?scoresobsdi sri:max ?maxscore .
    }
}
WHERE {
    {
        SELECT ?domain ?impact (sum(?max) as ?maxscore)
        WHERE {
            {   SELECT ?service ?impact (max(?score) as ?max)
                WHERE {
                    [] qb:dataSet sri:scores ;
                        sri:function ?service ;
                        sri:impact ?impact ;
                        sri:score ?score .
                    FILTER EXISTS {
                        GRAPH $scoresds {
                            ?scoresobsdi
                                qb:dataSet $scoresds ;
                                sri:function ?domain ;
                                sri:impact ?impact ;
                                sri:icscore ?discore .
                            $scoresds
                                qb:structure sri:dsd-scores ;
                                prov:wasDerivedFrom $ds .
                        }
                    }

                } GROUP BY ?impact ?service
            }
            ?domain skos:narrower ?service .
        } GROUP BY ?impact ?domain
    }

    GRAPH $scoresds {
        ?scoresobsdi
            qb:dataSet $scoresds ;
            sri:function ?domain ;
            sri:impact ?impact .
        $scoresds
            qb:structure sri:dsd-scores ;
            prov:wasDerivedFrom $ds .
    }
}
